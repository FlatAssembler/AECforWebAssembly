/*
 * Ovo je AEC program koji radi numeričke izračune vezane za Paradoks
 * rođendana, opisane ovdje: https://flatassembler.github.io/Karasica.doc
 */

// Uvezimo funkcije za debuggiranje iz JavaScripta...
Function printInt(Integer32 i) Which Returns Nothing Is External;
Function printString(PointerToCharacter s) Which Returns Nothing Is External;

Integer32 nasumican_broj := 9_907;
Function nasumican_broj() Which Returns Integer16 Does
	Return nasumican_broj := mod(nasumican_broj * 48_271, 2_147_483_647);
	// http://cplusplus.com/reference/random/minstd_rand/
EndFunction

Integer16 koliko_ljudi_ima_rodendan_na_taj_dan[1_000];
// Recimo da nikome neće trebati izračun za "godinu" s više od tisuću dana.

Function numericki_izracun_rodendanskog_paradoksa(
			Integer16 koliko_godina_ima_dana             ,
			Integer16 koliko_ima_ljudi                   ,
			Integer16 iznad_koliko_kolizija_brojimo      ,
			Integer32 koliko_smo_puta_izvrtili_simulaciju,
		) Which Returns Decimal32 Does
	Integer32 koliko_smo_puta_dobili_toliko_kolizija := 0,
		  vanjski_brojac                         := 0;
	While vanjski_brojac < koliko_smo_puta_izvrtili_simulaciju Loop
		Integer16 unutarnji_brojac := 0    ,
                	dani_za_debuggiranje[1_000];
		While unutarnji_brojac < koliko_godina_ima_dana Loop
			koliko_ljudi_ima_rodendan_na_taj_dan[
					unutarnji_brojac
				]        := 0;
			unutarnji_brojac += 1;
		EndWhile
		unutarnji_brojac := 0;
		While unutarnji_brojac < koliko_ima_ljudi Loop
			koliko_ljudi_ima_rodendan_na_taj_dan[
						mod(
							nasumican_broj()      ,
							koliko_godina_ima_dana,
						)
					] += 1; // AECforWebAssembly stariji
						// od v2.3.0 na ovu su
						// naredbu ispisivali krivi
						// asemblerski kod.
			Integer16 nasumican_dan := mod(
							nasumican_broj        ,
							koliko_godina_ima_dana,
						);
			dani_za_debuggiranje[
					unutarnji_brojac
				]         := nasumican_dan;
			unutarnji_brojac  += 1;
		EndWhile
		Integer16 jesmo_li_nasli_potreban_broj_kolizija := 0;
		unutarnji_brojac := 0;
		While unutarnji_brojac < koliko_godina_ima_dana Loop
			If
				koliko_ljudi_ima_rodendan_na_taj_dan[
					unutarnji_brojac
				] > iznad_koliko_kolizija_brojimo or
				koliko_ljudi_ima_rodendan_na_taj_dan[
					unutarnji_brojac
				] = iznad_koliko_kolizija_brojimo
			Then
				jesmo_li_nasli_potreban_broj_kolizija := 1;	
			EndIf
			unutarnji_brojac += 1;
		EndWhile
		If jesmo_li_nasli_potreban_broj_kolizija Then
			koliko_smo_puta_dobili_toliko_kolizija += 1;
		ElseIf koliko_ima_ljudi > koliko_godina_ima_dana Then
			printString("---");
			printString("Brojevi za debuggiranje:");
			unutarnji_brojac := 0;
			While unutarnji_brojac < koliko_ima_ljudi Loop
				printInt(dani_za_debuggiranje[
						unutarnji_brojac
					]);
				unutarnji_brojac += 1;
			EndWhile
			printString("Koliko ljudi ima rodendan na taj dan:");
			unutarnji_brojac := 0;
			While unutarnji_brojac < koliko_godina_ima_dana Loop
				printInt(koliko_ljudi_ima_rodendan_na_taj_dan[
						unutarnji_brojac
					]);
				unutarnji_brojac += 1;
			EndWhile
			printString("---");
		EndIf
		vanjski_brojac += 1;
	EndWhile
	Return Decimal32(
			koliko_smo_puta_dobili_toliko_kolizija
		) / koliko_smo_puta_izvrtili_simulaciju;
EndFunction
