/*
 * Ovo je AEC program koji radi numeričke izračune vezane za Paradoks
 * rođendana, opisane ovdje: https://flatassembler.github.io/Karasica.doc
 */

// Uvezimo funkcije za debuggiranje iz JavaScripta...
Function printInt(Integer32 i) Which Returns Nothing Is External;
Function printString(PointerToCharacter s) Which Returns Nothing Is External;

Function nasumican_broj() Which Returns Integer16 Is Declared;

Integer16 koliko_ljudi_ima_rodendan_na_taj_dan[1_000];
// Recimo da nikome neće trebati izračun za "godinu" s više od tisuću dana.

Function numericki_izracun_rodendanskog_paradoksa(
			Integer16 koliko_godina_ima_dana       ,
			Integer16 koliko_ima_ljudi             ,
			Integer16 iznad_koliko_kolizija_brojimo,
		) Which Returns Decimal32 Does
	Integer32 koliko_smo_puta_izvrtili_simulaciju  := 100_000,
		koliko_smo_puta_dobili_toliko_kolizija := 0      ,
		vanjski_brojac                         := 0      ;
	While vanjski_brojac < koliko_smo_puta_izvrtili_simulaciju Loop
		Integer16 unutarnji_brojac := 0,
                	dani_za_debuggiranje[1_000];
		While unutarnji_brojac < koliko_godina_ima_dana Loop
			koliko_ljudi_ima_rodendan_na_taj_dan[
					unutarnji_brojac
				]        := 0;
			unutarnji_brojac += 1;
		EndWhile
		unutarnji_brojac := 0;
		While unutarnji_brojac < koliko_ima_ljudi Loop
			Integer16 nasumican_dan := mod(
							nasumican_broj(),
							koliko_godina_ima_dana
						);
			koliko_ljudi_ima_rodendan_na_taj_dan[
							nasumican_dan
					] += 1;
			dani_za_debuggiranje[
					unutarnji_brojac
				]         := nasumican_dan;
			unutarnji_brojac  += 1;
		EndWhile
		Integer16 jesmo_li_nasli_potreban_broj_kolizija := 0;
		unutarnji_brojac := 0;
		While unutarnji_brojac < koliko_godina_ima_dana Loop
			If
				koliko_ljudi_ima_rodendan_na_taj_dan[
					unutarnji_brojac
				] > iznad_koliko_kolizija_brojimo or
				koliko_ljudi_ima_rodendan_na_taj_dan[
					unutarnji_brojac
				] = iznad_koliko_kolizija_brojimo
			Then
				jesmo_li_nasli_potreban_broj_kolizija := 1;	
			EndIf
			unutarnji_brojac += 1;
		EndWhile
		If jesmo_li_nasli_potreban_broj_kolizija Then
			koliko_smo_puta_dobili_toliko_kolizija += 1;
		ElseIf koliko_ima_ljudi > koliko_godina_ima_dana Then
			printString("---");
			unutarnji_brojac := 0;
			While unutarnji_brojac < koliko_godina_ima_dana Loop
				printInt(dani_za_debuggiranje[
						unutarnji_brojac
					]);
				unutarnji_brojac += 1;
			EndWhile
			printString("---");
		EndIf
		vanjski_brojac += 1;
	EndWhile
	Return Decimal32(
			koliko_smo_puta_dobili_toliko_kolizija
		) / koliko_smo_puta_izvrtili_simulaciju;
EndFunction

Integer32 nasumican_broj := 9_907;
Function nasumican_broj() Which Returns Integer16 Does
	Return nasumican_broj := mod(nasumican_broj * 48_271, 2_147_483_647);
	// http://cplusplus.com/reference/random/minstd_rand/
EndFunction
