PointerToCharacter elements[13]  :=   {"I",  "IV", "V",
                                       "IX", "X",  "XL",
                                       "L",  "XC", "C",
                                       "CD", "D",  "CM",
                                       "M"};
Integer16 meaningsOfElements[13] :=   { 1,    4,    5,
                                        9     10,   40,
                                        50,   90,  100,
                                        400,  500, 900,
                                        1000};
Character result[64];
//"Extern" has not been implemented (yet)...
Function pointerToResult() Which Returns PointerToCharacter Does
    Return AddressOf(result[0]);
EndFunction

Function strcat(PointerToCharacter dest, PointerToCharacter src)
         Which Returns Nothing Is Declared;

Function convertToRomanNumerals(Integer16 number) Which Returns Nothing Does
    result[0] := 0;
    PointerToCharacter result := pointerToResult();
    If number = 0 Then
        strcat(result,"N");
        Return;
    ElseIf number < 0 or number > 4000 - 1 Then
        strcat(result,"\"Please enter a number between 0 and 4000.\"");
        Return;
    EndIf
    Integer32 pointerToElement := 12; // To "M" (1000).
    While number Loop
        If number > meaningsOfElements[pointerToElement] - 1 Then
            strcat(result, elements[pointerToElement]);
            number -= meaningsOfElements[pointerToElement];
        Else
           pointerToElement -= 1;
        EndIf
    EndWhile
EndFunction

Function strlen(PointerToCharacter str) Which Returns Integer32 Does
    Integer32 length : = 0;
    While ValueAt(str + length) Loop
        length += 1;
    EndWhile
    Return length;
EndFunction

Function strcpy(PointerToCharacter dest,
                PointerToCharacter src) Which Returns Nothing Does
    While ValueAt(src) Loop
        ValueAt(dest) := ValueAt(src);
        dest          += 1;
        src           += 1;
    EndWhile
    ValueAt(dest) : = 0;
EndFunction

Function strcat(PointerToCharacter dest,
                PointerToCharacter src) Which Returns Nothing Does
    strcpy(dest + strlen(dest), src);
EndFunction
